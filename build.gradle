plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.gorylenko.gradle-git-properties' version '2.5.3'
    id 'jacoco'
}

group = 'com.boilerplate'
version = '0.0.1-SNAPSHOT'
description = 'springboot-java'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // ----- Spring Boot -----
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // Bean Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // ----- Lombok -----
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'

    // ----- JPA -----
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    // PostgreSQL DB Driver
    runtimeOnly 'org.postgresql:postgresql'

    // ----- Logging -----
    // JSON type logging
    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'

    // ----- Documentation -----
    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    // ----- Redis -----
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // ----- JWT -----
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly  'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly  'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // testContainer
    implementation 'org.testcontainers:postgresql'

    // testContainer for UnitTest
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:spock'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport  // 테스트 후 자동으로 리포트 생성
}

// JaCoCo 설정
jacoco {
    toolVersion = "0.8.11"
}

// JaCoCo 테스트 리포트 생성
jacocoTestReport {
    dependsOn test  // 테스트 실행 후 리포트 생성

    reports {
        xml.required = true
        html.required = true
        csv.required = false

        xml.outputLocation = file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
        html.outputLocation = file("${buildDir}/reports/jacoco/html")
    }

    afterEvaluate {
        // Lombok 생성 코드 제외
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/SpringbootJavaApplication.class',
                    '**/*Application.class',
                    '**/config/**',
                    '**/dto/**',
                    '**/entity/**'
            ])
        }))
    }
}

// 커버리지 검증
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70  // 70% 이상
            }
        }
    }
}

// ----- for /actuator/info -----
springBoot {
    buildInfo()
}

// ----- for /v1/info -----
gitProperties {
    keys = [
            'git.branch',
            'git.commit.id.abbrev',
            'git.commit.time',
            'git.commit.message.short'
    ]
}